@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# Find dependencies
find_dependency(Threads REQUIRED)
find_dependency(fmt REQUIRED)

# Optional dependencies based on build configuration
if(@USE_CUDA@)
    include(CheckLanguage)
    check_language(CUDA)
    if(CMAKE_CUDA_COMPILER)
        enable_language(CUDA)
    else()
        message(FATAL_ERROR "CUDA is required but no CUDA compiler found")
    endif()
endif()

if(@USE_BLAS@)
    find_dependency(BLAS REQUIRED)
    find_dependency(LAPACK REQUIRED)
endif()

if(@USE_MKL@)
    find_dependency(MKL REQUIRED)
endif()

if(@USE_OPENMP@)
    find_dependency(OpenMP REQUIRED)
endif()

# Include targets
include("${CMAKE_CURRENT_LIST_DIR}/BrezelTargets.cmake")

# Set configuration variables
set(BREZEL_VERSION @PROJECT_VERSION@)
set(BREZEL_CUDA_SUPPORT @USE_CUDA@)
set(BREZEL_BLAS_SUPPORT @USE_BLAS@)
set(BREZEL_MKL_SUPPORT @USE_MKL@)
set(BREZEL_OPENMP_SUPPORT @USE_OPENMP@)

# Check all required components are found
check_required_components(Brezel)